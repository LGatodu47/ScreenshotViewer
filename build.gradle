import java.nio.file.Paths

plugins {
    id 'fabric-loom' version '1.5-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name "Sonatype Maven"
        url "https://s01.oss.sonatype.org/content/repositories/releases"
    }
    maven {
        name "Mod Menu Maven Repo"
        url "https://maven.terraformersmc.com/releases"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

    def catConfigMc = "io.github.lgatodu47:catconfig-mc:${project.minecraft_version}-0.2.1"
    modImplementation catConfigMc
    include catConfigMc

    implementation "ca.weblite:java-objc-bridge:1.0.0"
    implementation 'io.github.lgatodu47:catconfig:0.3.0'
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

loom {
    accessWidenerPath = file("src/main/resources/screenshot_viewer.accesswidener")
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    libsDirName = Paths.get(libsDirName).resolve("${project.compatible_mc_version}").toString()
    archivesBaseName = project.archives_base_name
    version = version.concat("-${project.mod_loader}-mc${project.compatible_mc_version}")
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }

    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
}
